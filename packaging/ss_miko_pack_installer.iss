; 注意：请确保 MyAppVersion 与 localization 文件夹中的 localization_version.json 中的版本号一致！
#define MyAppVersion "R26.4f_097"
#define GameVersion "0.97a-RC11"
; 注意：请确保 OriginalGameFolder 指向Mikohime java兼容文件的路径
#define MikoFilesFolder "..\..\Mikohime_23_R26.4f_097"
#define MikoObfWithTranslationPath "..\localization\port_obf.jar"

; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define GameBaseName "Starsector(远行星号)"
#define TranslationPackName " " + GameVersion + " Java 23 适配包"
#define MyAppName GameBaseName + TranslationPackName
#define MyAppPublisher "Mikohime"
#define MyAppURL "https://www.fossic.org/"
#define TranslationProjectFolder "..\"
#define TargetInstallFolder "starsector-core"
#define RegistryDir "Software\Fractal Softworks\Starsector" 

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{B07914C8-14A1-4B7F-AAC6-EF74EA1EE51E}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
AllowCancelDuringInstall=no
DefaultDirName={reg:HKCU\{#RegistryDir},|{reg:HKLM\{#RegistryDir},|{autopf}\Starsector}}
DirExistsWarning=no
AppendDefaultDirName=false
SetupIconFile=miko_pack.ico
WizardSizePercent=100
WizardResizable=no
; 不要弄开始菜单
DisableProgramGroupPage=yes
DisableWelcomePage=no
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputBaseFilename={#MyAppName} {#MyAppVersion}
Compression=lzma2
SolidCompression=yes
WizardStyle=modern
WizardSmallImageFile=Wizard_SmallImage\Wizard_Image_Miko_Pack.bmp
; LicenseFile=PACK_LICENSE.txt
; 保证不要产生安装卸载程序
CreateUninstallRegKey=no
Uninstallable=no

 
[Languages]
Name: "chinesesimplified"; MessagesFile: "compiler:Languages\ChineseSimplified.isl"

[Messages]
chinesesimplified.SelectDirDesc= [name] 必须安装在 {#GameBaseName} 根目录下。
chinesesimplified.BrowseDialogLabel = 在下列列表中选择 {#GameBaseName} 安装文件夹，然后点击“确定”。

[Files]
Source: "{#MikoFilesFolder}\*";DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#MikoObfWithTranslationPath}"; DestDir: "{app}\mikohime"; Flags: ignoreversion 
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Code]
{ Below code warns end user if he tries to install into a folder that does not contain bzone.exe. Useful if user tries to install into addon or any non-BZ2 folder. }
function NextButtonClick(CurPageID: Integer): Boolean;
begin
  Log('NextButtonClick(' + IntToStr(CurPageID) + ') called');
  Result := True;
  case CurPageID of wpSelectDir:
    if not DirExists(ExpandConstant('{app}\{#TargetInstallFolder}')) then begin
      Log(ExpandConstant('{app}\{#TargetInstallFolder}'));
      MsgBox('请选择带有 {#TargetInstallFolder} 文件夹的 {#GameBaseName} 根目录！', mbError, MB_OK);
      Result := False;
    end else if not FileExists(ExpandConstant('{app}\{#TargetInstallFolder}\localization_version.json')) then begin
      Log(ExpandConstant('{app}\{#TargetInstallFolder}\localization_version.json'));
      MsgBox('选择的远行星号游戏未安装汉化包！{#MyAppName} 必须与汉化包一起使用。请首先安装汉化包或汉化版游戏。', mbError, MB_OK);
      Result := False;
    end;
  end;
end;